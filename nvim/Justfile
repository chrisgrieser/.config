set quiet := true

debugFile := "./debug/test.lua"
debugConfig := "./debug/repro.lua"
masonPath := "$HOME/.local/share/nvim/mason/bin/"

#───────────────────────────────────────────────────────────────────────────────

count-lines-in-subdirs:
    #!/usr/bin/env zsh
    find . -mindepth 2 -maxdepth 2 -type d | while read -r dir; do
        line_count=$(find "$dir" -name "*.lua" -type f -exec cat {} + | wc -l | tr -d ' ')
        dir=$(echo "$dir" | sed 's|\./||' | rs)
        [[ "$line_count" -eq 0 ]] || echo "$dir: $line_count"
    done

count-lines:
    #!/usr/bin/env zsh
    find . -name "*.lua" -not -path "./debug/*" -not -path "./templates/*" -print0 |
        xargs -0 wc -l |
        sort --reverse |
        cut -c5- |
        sed 's|\./||'

# for neovide, needs to add `"$HOME/.local/neovim-downgrade` to `.zprofile`
downgrade-nvim:
    #!/usr/bin/env zsh
    target_version="v0.11.2"
    rm -rf "$HOME/.local/neovim-downgrade"
    cd -q "$HOME/.local" || return 1
    curl --location --output "neovim-downgrade.tar.gz" \
        "https://github.com/neovim/neovim/releases/download/$target_version/nvim-macos-arm64.tar.gz"
    xattr -c "neovim-downgrade.tar.gz"
    tar xzvf "neovim-downgrade.tar.gz"
    rm "neovim-downgrade.tar.gz"
    mv "nvim-macos-arm64" "neovim-downgrade"

# runs minimal config with neovide
debug-instance:
    neovide --no-tabs {{ debugFile }} -- -u {{ debugConfig }} &>/dev/null

stylua-on-all:
    #!/usr/bin/env zsh
    {{ masonPath }}/stylua --check --output-format=summary . && return 0
    {{ masonPath }}/stylua .
    print "\nFiles formatted."
