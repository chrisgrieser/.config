set quiet

debugFile := "./debug/test.lua"
debugConfig := "./debug/repro.lua"

#───────────────────────────────────────────────────────────────────────────────

count-lines-in-subdirs:
    #!/usr/bin/env zsh
    total=0
    find . -mindepth 2 -maxdepth 2 -type d | while read -r dir; do
        line_count=$(find "$dir" -name "*.lua" -type f -exec cat {} + | wc -l | tr -d ' ')
        dir=$(echo "$dir" | sed 's|\./||' | rs)
        [[ "$line_count" -eq 0 ]] || echo "$dir: $line_count"
        total=$((total + line_count))
    done
    echo "Total: $total"

count-lines:
    #!/usr/bin/env zsh
    find . -name "*.lua" -not -path "./debug/*" -not -path "./templates/*" -print0 |
        xargs -0 wc -l |
        sort --reverse |
        cut -c5- |
        sed 's|\./||'

# for neovide, needs to add `$HOME/.local/neovim-downgrade` to `PATH` in `.zprofile`
downgrade-nvim target_version:
    #!/usr/bin/env zsh
    target_version=$(echo {{ target_version }} | sed 's/v//')
    rm -rf "$HOME/.local/neovim-downgrade"
    cd -q "$HOME/.local" || return 1
    curl --silent --location --output "neovim-downgrade.tar.gz" \
        "https://github.com/neovim/neovim/releases/download/$target_version/nvim-macos-arm64.tar.gz"
    xattr -c "neovim-downgrade.tar.gz"
    tar xzvf "neovim-downgrade.tar.gz"
    rm "neovim-downgrade.tar.gz"
    mv "nvim-macos-arm64" "neovim-downgrade"
    echo "Done downgrading to $target_version"

# runs minimal config with neovide
debug-instance:
    neovide --no-tabs {{ debugFile }} -- -u {{ debugConfig }} &>/dev/null
