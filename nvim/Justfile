set quiet := true

debugFile := "./debug/test.lua"
debugConfig := "./debug/repro.lua"
masonPath := "$HOME/.local/share/nvim/mason/bin/"

#───────────────────────────────────────────────────────────────────────────────

count-lines-in-subdirs:
    #!/usr/bin/env zsh
    find . -mindepth 2 -maxdepth 2 -type d | while read -r dir; do
        line_count=$(find "$dir" -name "*.lua" -type f -exec cat {} + | wc -l | tr -d ' ')
        dir=$(echo "$dir" | sed 's|\./||' | rs)
        [[ "$line_count" -eq 0 ]] || echo "$dir: $line_count"
    done

count-lines:
    #!/usr/bin/env zsh
    find . -name "*.lua" -not -path "./debug/*" -not -path "./templates/*" -print0 |
        xargs -0 wc -l |
        sort --reverse |
        cut -c5- |
        sed 's|\./||'

# runs minimal config with neovide
debug-instance:
    neovide --no-tabs {{ debugFile }} -- -u {{ debugConfig }} &>/dev/null

stylua-on-all:
    #!/usr/bin/env zsh
    {{ masonPath }}/stylua --check --output-format=summary . && return 0
    {{ masonPath }}/stylua .
    print "\nFiles formatted."
