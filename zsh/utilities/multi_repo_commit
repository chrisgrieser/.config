#!/usr/bin/env zsh

# CONFIG
github_username="chrisgrieser"
# set -e

# repo_names="alfred|shimmering-obsidian|gitfred"
# repo_names="nvim"
# repo_names="obsidian-quadro|pseudometa-obsidian-plugin-template|grappling-hook|obsidian-proofreader"
# ALL
repo_names="shimmering-focus|finder-vim-mode|zsh-magic-dashboard|nvim|alfred|shimmering-obsidian|gitfred|obsidian-quadro|pseudometa-obsidian-plugin-template|grappling-hook|obsidian-proofreader"

function actions_in_repo {
	if [[ -e "./.rumdl.toml" ]]; then
		cp -f "$HOME/.config/rumdl/rumdl-project-config.toml" "./.rumdl.toml"
		rumdl fmt
		rumdl fmt
	fi
}

commit_msg="style: include toc-validation in rumdl config"

amend="false" # no-edit|only-message|false

#â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€
#â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€
_export_github_token # defined in .zshenv

# GUARD
if [[ -z "$GITHUB_TOKEN" ]]; then
	print "\e[1;31mNo GitHub token found\e[0m"
	return 1
elif [[ ! -x "$(command -v gh)" ]]; then
	print "\e[1;33mgh not installed.\e[0m"
	return 1
fi

# only non-forked, non-archived repos
repos_to_update=$(gh repo list --limit=100 --no-archived --source --visibility=public \
	--json="name" --jq=".[].name | select(test(\"$repo_names\"))")
repo_count=$(echo "$repos_to_update" | wc -l | tr -d " ")

# confirmation that everything is correct
print "\e[1;34mRepos to update ($repo_count):\e[0m"
echo "$repos_to_update" | rs
echo

[[ "$amend" != "false" ]] && print "\e[1;33mWARN: amend $amend\e[0m"
if [[ "$amend" != "no-edit" ]]; then
	print "\e[1;34mCommit message:\e[0m"
	echo "$commit_msg"
fi
if [[ "$amend" != "only-message" ]]; then
	echo
	print "\e[1;34mActions:\e[0m"
	declare -f actions_in_repo | sed '1d;$d;s/^\t//' | bat --language=sh --no-pager
fi

echo
print "\e[1;34mProceed? (y/n)\e[0m"
read -r pressed
[[ "$pressed" != "y" ]] && return 1
echo
echo "â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€"
echo

#â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€

cd /tmp || return 1

# clone each repo, do the replacements, and git add/commit/push
i=1
not_updated=0
while read -r repo; do
	print "\e[1;34mUpdating $repoâ€¦ ($i/$repo_count)\e[0m"
	# WARN depth 5 relevant for prevent history loss when amending (extra safety net)
	git clone --no-progress --depth=5 "git@github.com:$github_username/$repo"
	cd "$repo" || return 1

	actions_in_repo

	# COMMIT/AMEND
	if [[ "$amend" == "no-edit" ]]; then
		git add --all && git commit --amend --no-edit && git push --force --no-progress
	elif [[ "$amend" == "only-message" ]]; then
		git add --all && git commit --amend --message="$commit_msg" && git push --force --no-progress
	else
		git add --all && git commit --message="$commit_msg" && git push --no-progress
	fi

	success=$?
	if [[ $success -ne 0 ]]; then
		print "\e[1;33mDid not update $repo.\e[0m"
		((not_updated++))
	fi

	echo
	cd ..
	rm -rf "$repo"
	((i++))
done <<< "$repos_to_update"

#â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€

if [[ $not_updated -gt 0 ]]; then
	echo "â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€â”€"
	print "\e[1;33m$not_updated repo(s) not updated.\e[0m"
fi
"$ZDOTDIR/notificator" --title "ðŸŒMulti-repo commit" --message "Update finished." --sound "Blow"
