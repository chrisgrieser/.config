#!/usr/bin/env zsh

# CONFIG
logpath_on_mac="$HOME/Library/Mobile Documents/com~apple~CloudDocs/Backups/backups-to-external-drives.log"

#───────────────────────────────────────────────────────────────────────────────
# DETERMINE VOLUME

spinner="⠋⠙⠹⠸⠼⠴⠦⠧⠇⠏"
for i in {1..200}; do
	volume_name="$(df | grep --only-matching " /Volumes/.*" |
		grep -v "/Volumes/Recovery" | cut -c2-)"
	if [[ $(echo "$volume_name" | wc -l) -gt 1 ]]; then
		print "\e[1;33mMore than one volume connected.\e[0m"
		return 1
	elif [[ -n "$volume_name" ]]; then
		break
	fi
	pos=$((i % ${#spinner}))
	printf "\rSearching for volume… %s" "${spinner:$pos:1}"
	sleep 0.2
done

echo
if [[ -z "$volume_name" ]]; then
	print "\e[1;33mTimeout, no volume found.\e[0m"
	return 1
else
	print "\e[1;34mBackup volume: $volume_name\e[0m"
	print "\e[1;38;5;247m─────────────────────────────────────────────────────────────────────────────"
fi

#───────────────────────────────────────────────────────────────────────────────
# DETERMINE BACKUP DESTINATION

device_name="$(scutil --get ComputerName)"
backup_dest="$volume_name/Backup_$device_name"
mkdir -p "$backup_dest"
cd "$backup_dest" || return 1
echo -n "Backup: $(date '+%Y-%m-%d %H:%M'), $volume_name -- " >> "$logpath_on_mac"

#───────────────────────────────────────────────────────────────────────────────
# HELPER FUNCTION

errors=""
function backup() {
	local bkp_from="$1"
	local bkp_to="$2"
	bkp_from=$(echo "$bkp_from" | sed -E 's|/?$|/|')

	if [[ ! -d "$bkp_from" ]]; then
		errors="$errors\n$bkp_from does not exist."
		return 1
	fi
	echo
	print "\e[1;34mBacking up: $bkp_from\e[0m"
	mkdir -p "$bkp_to"

	rsync --archive --delete-during --recursive --progress --human-readable \
		--exclude="*.Trash/*" --exclude="*/vim-data/*" "$bkp_from" "$bkp_to" ||
		errors="$errors\nProblems occurred for $bkp_from backup."

	print "\e[1;38;5;247m─────────────────────────────────────────────────────────────────────────────"
}

#───────────────────────────────────────────────────────────────────────────────
# CONFIG CONTENT TO BACKUP

backup "$HOME/Library/Mobile Documents/com~apple~CloudDocs/" ./iCloud-Folder
backup "$HOME/RomComs/" ./Homefolder/RomComs
backup "$HOME/Documents/" ./Homefolder/Documents # backed up from home as iCloud only has symlinks
backup "$HOME/Library/Preferences/" ./Homefolder/Library/Preferences

# also backup all PERMA-REPOS
while read -r line; do
	repo_path=$(echo "$line" | cut -d, -f2 | sed "s|^~|$HOME|")
	basename="$(basename "$repo_path")"
	backup "$repo_path/" "./perma-repos/$basename"
done < "$HOME/.config/perma-repos.csv"

# contains info on all the vaults, so they do not have to be added manually
cp -f "$HOME/Library/Application Support/obsidian/obsidian.json" ./perma-repos/obsidian.json

#───────────────────────────────────────────────────────────────────────────────
# LOG & NOTIFY

echo
if [[ -z "$errors" ]]; then
	print "\e[1;32mBackup on $volume_name completed.\033[0m"
	"$ZDOTDIR/notificator" --title "Backup" --message "✅ completed." --sound "Blow"

	echo "completed: $(date '+%H:%M')" >> "$logpath_on_mac"
	echo "Backup: $(date '+%Y-%m-%d %H:%M')" >> "$backup_dest/last_backup.log"
else
	print "\e[1;31m$errors\033[0m"
	"$ZDOTDIR/notificator" --title "Backup" --message "⚠️ Errors occurred." --sound "Basso"

	echo "had errors: $(date '+%H:%M')" >> "$logpath_on_mac"
fi
